// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    
    Inc16(in=out2, out=incout);
    Mux16(a=out2, b=incout, sel=inc, out=preout);
    Mux16(a=preout, b=in, sel=load, out=preout2);
    Mux16(a=preout2, b=false, sel=reset, out=preout3);

    Or(a=load, b=reset, out=load1);
    Or(a=load1, b=inc, out=load2);
    Register(in=preout3, load=load2, out=out, out=out2);
}
